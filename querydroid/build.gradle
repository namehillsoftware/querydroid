plugins {
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

apply plugin: 'com.android.library'
apply plugin: 'signing'

def isRelease = true

def versionString = {
    try {
        def gitDescribe = providers.exec {
            it.ignoreExitValue = true
            commandLine("git", "describe")
        }.standardOutput.asText.get().trim()

        def latestTag = providers.exec {
            it.ignoreExitValue = true
            commandLine("git", "describe", "--abbrev=0")
        }.standardOutput.asText.get().trim()

        if (gitDescribe != latestTag) {
            isRelease = false
            return latestTag + "-SNAPSHOT"
        }

        isRelease = true
        return gitDescribe
    } catch (ignored) {
        return "0.0.1"
    }
}()

android {
    namespace = "com.namehillsoftware.querydroid"

    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 33
        versionName = versionString
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                maxHeapSize = "2048m"
            }
        }
    }

    buildTypes {
        release {
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.namehillsoftware:lazy-j:0.11.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.robolectric:robolectric:4.10.3'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    testImplementation 'androidx.test:core:1.5.0'
}

// If you want to publish your sources as well
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

def signingKey = findProperty("signingKey")?.replace("\\n", "\n")
def signingPassword = findProperty("signingPassword")

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from(components.release)

                groupId = "com.namehillsoftware"
                artifactId = 'querydroid'
                version = versionString

                artifact androidSourcesJar

                pom {
                    name = 'querydroid'
                    description = 'Map SQLite Java objects.'
                    url = 'https://github.com/namehillsoftware/querydroid'
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://spdx.org/licenses/MIT.html"
                        }
                    }
                    developers {
                        developer {
                            id = 'david'
                            name = 'David Vedvick'
                            email = 'dvedvick@gmail.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/namehillsoftware/querydroid'
                    }
                }
            }
        }
        repositories {
            maven {
                // change URLs to point to your repos, e.g. http://my.org/repo
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = isRelease ? releasesRepoUrl : snapshotsRepoUrl
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

signing {
    required { signingKey?.trim() && gradle.taskGraph.hasTask("publish") }

    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications
}